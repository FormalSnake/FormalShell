{ lib, stdenvNoCC, rustCommon, makeBinaryWrapper, lucide-font, nerd-fonts, noto-fonts-color-emoji, matugen, brightnessctl, ddcutil, wlsunset
, wireplumber, cava, mpv, curl, util-linux, coreutils, procps, systemd, glib, pipewire, asusctl, uxplay, iphone-bridge, openscq30, nothingctl, earbuds
, formalshell-eds, git, qrencode, networkmanager, wl-clipboard, grim, slurp, wf-recorder, tesseract, ffmpeg-headless, pulseaudio, xdg-utils
, tensaku, ttfx, clipssh, localsend-cli, wtype, openssh }:

rustCommon.craneLib.buildPackage (rustCommon.commonArgs // {
  inherit (rustCommon) cargoArtifacts cargoVendorDir;
  pname = "formalshell";
  version = "0.1.1";

  # fs-theme embeds the chrome tables the QML shell reads too, one of its
  # tests reads Core/Theme.qml, and the theme service's tests run matugen
  # shims against the shell's own templates.
  src = lib.fileset.toSource {
    root = ../.;
    fileset = lib.fileset.unions [
      ../crates
      ../shell/Theme/themes
      ../shell/Theme/icons
      ../shell/Theme/templates
      ../shell/Core/Theme.qml
      ../shell/Menu/default-menu.jsonc
      ../shell/Menu/emoji.json
      ../shell/Radio/countries.json
    ];
  };
  postUnpack = ''
    cd $sourceRoot/crates
    sourceRoot="."
  '';

  # The pure library crates read repo fixtures (shell/, tests/) that this
  # crates-only source does not carry, so the runtime package builds and tests
  # itself alone; `cargo test` at the workspace root covers the rest.
  cargoExtraArgs = "--locked --package formalshell-rs";

  nativeBuildInputs = rustCommon.commonArgs.nativeBuildInputs ++ [ makeBinaryWrapper ];

  # The icon fonts by path, registered with parley at startup.
  #
  # Suffixed rather than prefixed: anything a host install or the smoke rig's
  # PATH shims must be able to shadow (wtype, ssh and clipssh, which the
  # --spaces and --clipssh legs replace; uxplay, localsend-cli and the iPhone
  # bridge, every caller guarding with `command -v`), and the clients of a
  # daemon the host runs, which have to match that daemon's own build
  # (nmcli, pw-dump, busctl and systemctl, earbuds, asusctl).
  # util-linux is here for setpriv: every long-lived child is started under
  # PR_SET_PDEATHSIG, with no fallback when it is missing.
  #
  # The examples directory carries the Hyprland config the home-manager
  # module links (binds and layer rules); nothing in the shell reads it.
  postInstall = ''
    mkdir -p $out/share/formalshell
    cp -r --no-preserve=mode ${../shell/Theme/templates} $out/share/formalshell/templates
    cp -r --no-preserve=mode ${../branding} $out/share/formalshell/branding
    cp -r --no-preserve=mode ${../docs/examples} $out/share/formalshell/examples
    wrapProgram $out/bin/formalshell-rs \
      --set-default FS_RS_ICON_FONT ${lucide-font}/share/fonts/truetype/lucide.ttf \
      --set-default FS_RS_FONT_DIRS ${nerd-fonts.symbols-only}/share/fonts:${noto-fonts-color-emoji}/share/fonts \
      --set-default FS_TEMPLATE_DIR $out/share/formalshell/templates \
      --set-default FS_BRANDING_DIR $out/share/formalshell/branding \
      --prefix PATH : ${lib.makeBinPath [ matugen brightnessctl ddcutil wlsunset wireplumber cava mpv curl util-linux procps git formalshell-eds qrencode wl-clipboard grim slurp wf-recorder tesseract ffmpeg-headless pulseaudio xdg-utils ttfx ]} \
      --suffix PATH : ${lib.makeBinPath ([ tensaku wtype openssh clipssh localsend-cli uxplay iphone-bridge networkmanager pipewire systemd glib ]
        ++ lib.optional (lib.meta.availableOn stdenvNoCC.hostPlatform asusctl) asusctl
        ++ lib.optionals (lib.meta.availableOn stdenvNoCC.hostPlatform earbuds) [ earbuds openscq30 ]
        ++ lib.optional (lib.meta.availableOn stdenvNoCC.hostPlatform nothingctl) nothingctl)}
    ln -s formalshell-rs $out/bin/formalshell

    # Liveness probe for the home-manager module's formalshell-watchdog timer
    # (packaging/formalshell-watchdog.in has the why).
    substitute ${../packaging/formalshell-watchdog.in} $out/bin/formalshell-watchdog \
      --subst-var-by IPC $out/bin/formalshell-ipc \
      --subst-var-by TIMEOUT ${lib.getExe' coreutils "timeout"} \
      --subst-var-by SYSTEMCTL ${lib.getExe' systemd "systemctl"}
    chmod +x $out/bin/formalshell-watchdog
  '';

  meta = {
    description = "FormalShell, a Hyprland desktop shell";
    license = lib.licenses.mit;
    mainProgram = "formalshell";
    platforms = lib.platforms.linux;
  };
})
